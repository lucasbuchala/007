my ast;
if !ast {
    ast = new Q.CompUnit {
        block: new Q.Block {
            parameterlist: new Q.ParameterList {},
            statementlist: new Q.StatementList {
                statements: []
            }
        }
    };
}

my NO_OUTER = {};

my Runtime = {
    new() {
        my frames = [];
        func enter(outer_frame, static_lexpad) {
            my frame = { outer_frame, pad: {} };
            frames.push(frame);
            for static_lexpad.keys() -> name {
                my value = static_lexpad[name];
                my identifier = new Q.Identifier { name };
                declare_var(identifier, value);
            }
        }
        func leave() {
            frames.pop();
        }
        func current_frame() {
            if !frames {
                return NO_OUTER;
            }
            return frames[frames.size() - 1];
        }

        func find_pad(symbol) {
            my frame = current_frame();
            # The below check didn't work
            # while frame.id != NO_OUTER.id {
            # So trying this instead:
            while frame.has("pad") {
                if frame.pad.has(symbol) {
                    return frame.pad;
                }
                frame = frame.outer_frame;
            }
            throw new Exception { message: "Cannot find variable '" ~ symbol ~ "'" };
        }
        func get_var(name) {
            my pad = find_pad(name);
            return pad[name];
        }
        func put_var(name, value) {
            my pad = find_pad(name);
            pad[name] = value;
        }
        func declare_var(identifier, value) {
            my name = identifier.name;
            # XXX: don't know if we should expose identifier.frame in 007
            # my frame = identifier.frame || current_frame();
            my frame = current_frame();
            frame.pad[name] = value;
        }

        func sigbind(c, arguments) {
            my paramcount = c.parameterlist.parameters.size();
            my argcount = arguments.size();
            if paramcount != argcount {
                throw new Exception { message: "parameter mismatch" };
            }
            enter(c["outer-frame"], c["static-lexpad"]);
            for ^argcount -> i {
                my param = c.parameterlist.parameters[i];
                my arg = arguments[i];
                declare_var(param.identifier, arg);
            }
        }

        func call(c, arguments) {
            sigbind(c, arguments);
            my frame = current_frame();
            run(c.statementlist);
            leave();
        }

        my eval_of_type = {
            "Q.Identifier": func(ident) {
                return get_var(ident.name);
            },
            "Q.Infix.Addition": func(op) {
                return eval(op.lhs) + eval(op.rhs);
            },
            "Q.Infix.And": func(op) {
                return eval(op.lhs) && eval(op.rhs);
            },
            "Q.Infix.Assignment": func(op) {
                my value = eval(op.rhs);
                if op.lhs ~~ Q.Identifier {    # XXX: this assumption does not always hold
                    put_var(op.lhs.name, value);
                }
                return value;
            },
            "Q.Infix.Concat": func(op) {
                return eval(op.lhs) ~ eval(op.rhs);
            },
            "Q.Infix.Divisibility": func(op) {
                return eval(op.lhs) %% eval(op.rhs);
            },
            "Q.Infix.Eq": func(op) {
                return eval(op.lhs) == eval(op.rhs);
            },
            "Q.Infix.Ge": func(op) {
                return eval(op.lhs) >= eval(op.rhs);
            },
            "Q.Infix.Gt": func(op) {
                return eval(op.lhs) > eval(op.rhs);
            },
            "Q.Infix.Le": func(op) {
                return eval(op.lhs) <= eval(op.rhs);
            },
            "Q.Infix.Lt": func(op) {
                return eval(op.lhs) < eval(op.rhs);
            },
            "Q.Infix.Multiplication": func(op) {
                return eval(op.lhs) * eval(op.rhs);
            },
            "Q.Infix.Modulo": func(op) {
                return eval(op.lhs) % eval(op.rhs);
            },
            "Q.Infix.Ne": func(op) {
                return eval(op.lhs) != eval(op.rhs);
            },
            "Q.Infix.Or": func(op) {
                return eval(op.lhs) || eval(op.rhs);
            },
            "Q.Infix.Subtraction": func(op) {
                return eval(op.lhs) - eval(op.rhs);
            },
            "Q.Infix.TypeMatch": func(op) {
                return eval(op.lhs) ~~ eval(op.rhs);
            },
            "Q.Literal.Int": func(lit_int) {
                return lit_int.value;
            },
            "Q.Literal.Str": func(lit_str) {
                return lit_str.value;
            },
            "Q.Postfix.Call": func(op) {
                # XXX: short-term hack to get `say` early; needs to go away
                if op.operand ~~ Q.Identifier && op.operand.name == "say" {
                    return say(eval(op.argumentlist.arguments[0]));
                }
                my c = eval(op.operand);
                if c ~~ Macro {
                    throw new Exception { message: "macro is called at runtime" };
                }
                if c !~~ Func {
                    throw new Exception { message: "Trying to invoke a " ~ type(c).name };
                }
                my arguments = op.argumentlist.arguments.map(eval);
                return call(c, arguments);
            },
            "Q.Postfix.Index": func(op) {
                return eval(op.operand)[eval(op.index)];
            },
            "Q.Postfix.Property": func(op) {
                return eval(op.operand)[eval(op.property)];
            },
            "Q.Prefix.Minus": func(op) {
                return -eval(op.operand);
            },
            "Q.Prefix.So": func(op) {
                return ?eval(op.operand);
            },
            "Q.Prefix.Not": func(op) {
                return !eval(op.operand);
            },
            "Q.Prefix.Upto": func(op) {
                return ^eval(op.operand);
            },
            "Q.Term.Array": func(array) {
                return array.elements.map(eval);
            },
            "Q.Term.Func": func(term) {
                my name = term.identifier && term.identifier.name || "";
                return new Func {
                    name,
                    parameterlist: term.block.parameterlist,
                    statementlist: term.block.statementlist,
                    "static-lexpad": term.block["static-lexpad"],
                    "outer-frame": current_frame(),
                };
            },

            # these were added to be able to run more tests
            # please move and implement as necessary
            "Q.Block": func(block) {
            },
            "Q.Infix": func(op) {
            },
            "Q.Postfix": func(op) {
            },
            "Q.Prefix": func(op) {
            },
            "Q.Term.Object": func(term) {
            },
            "Q.Term.Quasi": func(term) {
            },
            "Q.Expr.BlockAdapter": func(adapter) {
            },
        };
        func eval(q) { return eval_of_type[type(q).name](q); }

        my run_of_type = {
            "Q.Statement.BEGIN": func(stmt) {
                # no runtime behavior
            },
            "Q.CompUnit": func(compunit) {
                enter(current_frame(), compunit.block["static-lexpad"]);
                run(compunit.block.statementlist);
                leave();
            },
            "Q.Statement.Block": func(stmt) {
                enter(current_frame(), stmt.block["static-lexpad"]);
                run(stmt.block.statementlist);
                leave();
            },
            "Q.Statement.Expr": func(stmt) {
                eval(stmt.expr);
            },
            "Q.Statement.For": func(stmt) {
                my array = eval(stmt.expr);

                for array -> arg {
                    enter(current_frame(), stmt.block["static-lexpad"]);
                    my parameters = stmt.block.parameterlist.parameters;
                    if parameters {
                        declare_var(parameters[0].identifier, arg);
                    }
                    run(stmt.block.statementlist);
                    leave();
                }
            },
            "Q.Statement.If": func(stmt) {
                my expr = eval(stmt.expr);
                if expr {
                    enter(current_frame(), stmt.block["static-lexpad"]);
                    my parameters = stmt.block.parameterlist.parameters;
                    if parameters {
                        declare_var(parameters[0].identifier, expr);
                    }
                    run(stmt.block.statementlist);
                    leave();
                }
            },
            "Q.Statement.Macro": func(stmt) {
                # no runtime behavior
            },
            "Q.Statement.My": func(stmt) {
                my name = stmt.identifier.name;
                if stmt.expr {
                    put_var(name, eval(stmt.expr));
                }
            },
            "Q.Statement.Func": func(stmt) {
                # no runtime behavior
            },
            "Q.Statement.While": func(stmt) {
                my expr;
                while expr = eval(stmt.expr) {
                    enter(current_frame(), stmt.block["static-lexpad"]);
                    my parameters = stmt.block.parameterlist.parameters;
                    if parameters {
                        declare_var(parameters[0].identifier, expr);
                    }
                    run(stmt.block.statementlist);
                    leave();
                }
            },
            "Q.StatementList": func(stmtlist) {
                for stmtlist.statements -> statement {
                    run(statement);
                }
            },

            # these were added to be able to run more tests
            # please move and implement as necessary
            "Q.Statement.Return": func(stmt) {
            },
        };
        func run(q) { run_of_type[type(q).name](q); }

        return { run, get_var, put_var };
    }
};

my runtime = Runtime.new();
runtime.run(ast);
